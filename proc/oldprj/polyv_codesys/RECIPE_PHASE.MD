```pascal
FUNCTION RECIPE : INT
VAR_IN_OUT
	CFG: CTRLRECIPE;
	CTRL: PHASE_CTRL;
END_VAR
VAR
	STA: int;
	CMD: int;
	AUTO: bool;
	SEMI: bool;
	INBUF: bool;
	MAN: bool;
	CMD_ABORT: bool;
	CMD_HOLD: bool;
	CMD_PAUSE: bool;
	CMD_RESET: bool;
	CMD_RESTART: bool;
	CMD_RESUME: bool;
	CMD_START: bool;
	CMD_STOP: bool;
	TMAXERR: bool;
	TMINERR: bool;
	ALM: bool;
	AMAXENBL: BOOL;
	AMINENBL: BOOL;
END_VAR

//пдібно до PHASE
STA := CFG.STA;
TMAXERR:=STA.8;
TMINERR:=STA.9;
ALM:=STA.10;
SEMI := STA.11;
MAN := STA.13;

AUTO := NOT MAN AND NOT SEMI;
CMD:=CFG.CMD;


//вибір джерела команди керування станом в залежності від режиму
CMD_ABORT := CTRL.CMD_ABORT OR CMD=8 ;
CMD_HOLD := CTRL.CMD_HOLD OR CMD=6;
CMD_PAUSE := CTRL.CMD_PAUSE OR CMD=3;
CMD_RESET := CTRL.CMD_RESET OR CMD=4;
CMD_RESTART := CTRL.CMD_RESTART OR CMD=5;
CMD_RESUME := CTRL.CMD_RESUME OR CMD=2;
CMD_START := CTRL.CMD_START OR CMD=1;
CMD_STOP := CTRL.CMD_STOP OR CMD=7;

CASE CFG.STEP1 OF	
	0://0 - ініціалізація
	CFG.STEP1:=1;	
	1://1 – Idle (очікування)
	CFG.TSTEP1:=0;	TMAXERR:=FALSE; TMINERR:=FALSE; 
	IF CMD_START AND CTRL.ENBL THEN CFG.STEP1:=2; END_IF;
	2://2 - Running
	IF CTRL.RUNING_CMPLT THEN 
		CFG.STEP1:=8;
	END_IF;
	IF CMD_PAUSE THEN CFG.STEP1:=3; END_IF;
	IF CMD_HOLD THEN CFG.STEP1:=5; END_IF;
	IF CMD_STOP THEN CFG.STEP1:=9; END_IF;
	IF CMD_ABORT THEN CFG.STEP1:=11; END_IF;
	3://3 – Pausing
	IF CTRL.PAUSING_CMPLT THEN CFG.STEP1:=4; END_IF;
	IF CMD_HOLD THEN CFG.STEP1:=5; END_IF;
	IF CMD_STOP THEN CFG.STEP1:=9; END_IF;
	IF CMD_ABORT THEN CFG.STEP1:=11; END_IF;	
	4://4 – Paused
	IF CMD_RESUME THEN CFG.STEP1:=2; END_IF;
	IF CMD_HOLD THEN CFG.STEP1:=5; END_IF;
	IF CMD_STOP THEN CFG.STEP1:=9; END_IF;
	IF CMD_ABORT THEN CFG.STEP1:=11; END_IF;		
	5://5 – Holding
	IF CTRL.HOLDING_CMPLT THEN CFG.STEP1:=6; END_IF;
	IF CMD_STOP THEN CFG.STEP1:=9; END_IF;
	IF CMD_ABORT THEN CFG.STEP1:=11; END_IF;			
	6://6 – Hold
	CFG.TSTEP1:=0;
	IF CMD_RESTART THEN CFG.STEP1:=7; END_IF;	
	IF CMD_STOP THEN CFG.STEP1:=9; END_IF;
	IF CMD_ABORT THEN CFG.STEP1:=11; END_IF;		
	7://7 – Restarting
	IF CTRL.RESTARTING_CMPLT THEN CFG.STEP1:=2; END_IF;
	IF CMD_STOP THEN CFG.STEP1:=9; END_IF;
	IF CMD_ABORT THEN CFG.STEP1:=11; END_IF;			
	8://8 – Complete
	IF CMD_RESET THEN CFG.STEP1:=1; END_IF;	
	9://9 – Stopping
	IF CTRL.STOPPING_CMPLT THEN CFG.STEP1:=10; END_IF;
	IF CMD_ABORT THEN CFG.STEP1:=11; END_IF;		
	10://10 – Stopped
	IF CMD_RESET THEN CFG.STEP1:=1; END_IF;		
	11://11 - Aborting
	IF CTRL.ABORTING_CMPLT THEN CFG.STEP1:=12; END_IF;		
	12://11 - Aborted
	IF CMD_RESET THEN CFG.STEP1:=1; END_IF;	
	ELSE
	CFG.STEP1:=0;
END_CASE;


CTRL.STA_IDLE:=(CFG.STEP1=1);
CTRL.STA_RUNNING:=(CFG.STEP1=2);
CTRL.STA_PAUSING:=(CFG.STEP1=3);
CTRL.STA_PAUSED:=(CFG.STEP1=4);
CTRL.STA_HOLDING:=(CFG.STEP1=5);
CTRL.STA_HOLD:=(CFG.STEP1=6);
CTRL.STA_RESTARTING:=(CFG.STEP1=7);
CTRL.STA_COMPLETE:=(CFG.STEP1=8);
CTRL.STA_STOPPING:=(CFG.STEP1=9);
CTRL.STA_STOPPED:=(CFG.STEP1=10);
CTRL.STA_ABORTING:=(CFG.STEP1=11);
CTRL.STA_ABORTED:=(CFG.STEP1=12);

CTRL.CMD_ABORT := FALSE;
CTRL.CMD_HOLD := FALSE;
CTRL.CMD_PAUSE := FALSE;
CTRL.CMD_RESET := FALSE;
CTRL.CMD_RESTART := FALSE;
CTRL.CMD_RESUME := FALSE;
CTRL.CMD_START := FALSE;
CTRL.CMD_STOP := FALSE;

ALM:=TMINERR OR TMAXERR;
STA:=CFG.STEP1;
STA.8:=TMAXERR;
STA.9:=TMINERR;
STA.10:=ALM;
STA.11:=SEMI;
STA.12:=INBUF;
STA.13:=MAN;

CFG.CMD:=0; 
CFG.STA:=STA; 

IF PLC.PLS.P1S AND CFG.STEP1=2 THEN //рахувати тільки в стані RUNNING, обнуляється також при холдінгу
    CFG.TSTEP1:=CFG.TSTEP1+1;
    IF CFG.TSTEP1>30000 THEN CFG.STEP1:=30000;END_IF;     
END_IF;
```